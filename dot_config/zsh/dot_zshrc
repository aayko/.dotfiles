source $ZDOTDIR/plugins/zsh-autosuggestions/zsh-autosuggestions.plugin.zsh
source $ZDOTDIR/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
source $ZDOTDIR/plugins/zsh-history-substring-search/zsh-history-substring-search.plugin.zsh

source $ZDOTDIR/fzf.zsh
source $ZDOTDIR/completion.zsh
source $ZDOTDIR/opts.zsh
source $ZDOTDIR/aliases.sh
source $ZDOTDIR/set_prompt.sh

mkdir -p "$XDG_STATE_HOME"/zsh > /dev/null
export HISTFILE="$XDG_STATE_HOME"/zsh/history 
HISTSIZE=50000
SAVEHIST=50000

# don't add failed command to history
zshaddhistory() { whence ${${(z)1}[1]} >| /dev/null || return 1 }

zle_highlight=('paste:none')
stty ixany

# ------ WIDGETS ------

function lfcd () {
    tmp="$(mktemp)"
    command lf -last-dir-path="$tmp" "$@"
    if [ -f "$tmp" ]; then
        dir="$(cat "$tmp")"
        \rm -f "$tmp"
        if [ -d "$dir" ]; then
            if [ "$dir" != "$(pwd)" ]; then
                cd "$dir"
                set_prompt
            fi
        fi
    fi
    [[ $WIDGET == lfcd ]] && zle reset-prompt
}
zle -N lfcd

function prepend-sudo {
    if [[ $BUFFER != "sudo "* ]]; then
        BUFFER="sudo $BUFFER"; CURSOR+=5
    fi
}
zle -N prepend-sudo

autoload -U edit-command-line
zle -N edit-command-line

zmodload -i zsh/parameter
insert-last-command-output() {
    LBUFFER+="$(eval $history[$((HISTCMD-1))])"
}
zle -N insert-last-command-output

# ------ BINDS ------
bindkey -e

bindkey '\ev' edit-command-line
bindkey '\es' prepend-sudo

bindkey '\ec' fzf-cd-current
bindkey '^R' fzf-history-widget
bindkey '^T' fzf-cd-config
bindkey '^J' fzf-cd-projects

bindkey '^K' fzf-file-widget

bindkey '^O' lfcd

bindkey '^P' history-substring-search-up
bindkey '^N' history-substring-search-down
HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1

# shift-tab in completion
bindkey '^[[Z' reverse-menu-complete

bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char

bindkey -M menuselect '?' history-incremental-search-forward
bindkey -M menuselect '/' history-incremental-search-backward

precmd() {
    set_prompt
}
